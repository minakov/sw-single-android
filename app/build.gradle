import com.android.build.OutputFile

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

ext.abiCodes = ['armeabi-v7a':1, x86:2]

android {
    compileSdkVersion 27
    flavorDimensions "api"

    defaultConfig {
        targetSdkVersion 27

        versionCode 1
        versionName "1.0.1"
        applicationId "com.skywindgroup.lobby"

        resConfigs "en"
        buildConfigField "String", "LOG_TAG", "\"Lobby\""

        vectorDrawables {
            useSupportLibrary = true
        }
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    productFlavors {
        api21 {
            dimension "api"
            minSdkVersion 21
            versionCode 20000 + android.defaultConfig.versionCode
        }
        api16 {
            dimension "api"
            minSdkVersion 16
            versionCode 10000 + android.defaultConfig.versionCode
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    splits {
        abi {
            enable true
            reset()
            include "x86", "armeabi-v7a"
        }
    }
    applicationVariants.all { variant ->
        // Assigns a different version code for each output APK other than the universal APK.
        variant.outputs.each { output ->
            def baseAbiVersionCode = project.ext.abiCodes.get(output.getFilter(OutputFile.ABI))
            if (baseAbiVersionCode != null) {
                output.versionCodeOverride = baseAbiVersionCode + variant.versionCode
            }
        }
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
    implementation 'com.android.support:appcompat-v7:27.0.2'
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'

    api16Implementation 'org.xwalk:xwalk_core_library:23.53.589.4'
}
